plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'java'
    id 'application'
    id 'idea'
    id 'checkstyle'
}

repositories {
    mavenCentral()
    //mavenLocal()
    maven {
        url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots")
    }
}

dependencies {

    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.junit.platform:junit-platform-launcher:1.9.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.3"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.9.3"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.3"
    testImplementation "com.google.code.gson:gson:2.10.1"
    testImplementation "org.yaml:snakeyaml:2.0"
    testImplementation("com.google.truth:truth:1.1.3")

    implementation("org.jspecify:jspecify:0.3.0")
    implementation "info.picocli:picocli:4.7.3"

    implementation 'io.github.jmltoolkit:jmlparser-symbol-solver-core:3.25.10-b3-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

}


tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}


java {
    sourceCompatibility = JavaVersion.VERSION_21
}


application {
    mainClass = "cli.Main"
}

checkstyle {
    toolVersion "8.45.1"
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}


tasks.register('harshCheckstyle', Checkstyle) {
    source = "src/"
    configFile = file("${rootDir}/config/checkstyle/checkstyleHarsh.xml")
    classpath = files()
}

tasks.register('caseStudies', JavaExec) {
    classpath sourceSets.test.runtimeClasspath
    mainClass = "casestudy.CaseStudies"
}

tasks.register('runTmpTest', JavaExec) {
    mainClass = "cli.Main"
    classpath = sourceSets.main.runtimeClasspath
    args = ["./testRes/CaseStudyCandidates/TmpTest.java", "-kt", "-d", "-tr"]
}