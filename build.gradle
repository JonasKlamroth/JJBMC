/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4.1/userguide/tutorial_java_projects.html
 */


// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'checkstyle'

sourceCompatibility = 17
test.testLogging.showStandardStreams = true

checkstyle {
    toolVersion '8.45.1'    // your choice here
}

checkstyleMain {
    source ='src/'
}

checkstyleTest {
    source ='tests/'
}

test {
    useJUnitPlatform()
}


repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots")
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation("com.google.code.gson:gson:2.10.1")
    testImplementation 'org.yaml:snakeyaml:2.0'

    implementation 'info.picocli:picocli:4.7.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'

    implementation 'io.github.jmltoolkit:jmlparser-core:3.25.1-SNAPSHOT'
    implementation 'io.github.jmltoolkit:jmlparser-symbol-solver-core:3.25.1-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    testImplementation("com.google.truth:truth:1.1.3")

    manifest {
        attributes "Main-Class": "test.TestRunner"
    }
}

task fatJar(type: Jar) {
    dependsOn tasks.getByPath("classes")

    manifest {
        attributes "Main-Class": "cli.Main"
    }
    archiveBaseName = 'JJBMC'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    doLast {
        copy {
            from 'build/libs/JJBMC.jar'
            into '.'
        }
    }
}

task testJar(type: Jar) {
    dependsOn tasks.getByPath("classes")
    dependsOn tasks.getByPath("testClasses")

    manifest {
        attributes "Main-Class": "test.TestRunner"
    }
    archiveBaseName = 'JJBMC-test'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    sourceSets.main.output.each {
        from it
    }
    sourceSets.test.output.each {
        from it
    }
    from {
        configurations.testRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }


    with jar
    doLast {
        copy {
            from 'build/libs/JJBMC-test.jar'
            into '.'
        }
    }
}

task harshCheckstyle(type: Checkstyle) {
    source = "src/"
    configFile = file("${rootDir}/config/checkstyle/checkstyleHarsh.xml")
    classpath = files()
}

task caseStudies(type: JavaExec) {
    classpath sourceSets.test.runtimeClasspath
    mainClass = "casestudy.CaseStudies"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}